buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "com.gradle.publish:plugin-publish-plugin:0.11.0"
  }
}

apply plugin: "com.gradle.plugin-publish"
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven-publish'

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'com.amazonaws:aws-java-sdk-s3:1.11.751'
    testCompile 'junit:junit:4.12'
}

group = 'com.yantranet.gradle'
version = '1.1.0'

pluginBundle {
    website = 'https://github.com/vishnuganta22/gradle-android-s3'
    vcsUrl = 'https://github.com/vishnuganta22/gradle-android-s3'
    description = 'A simple light weight Gradle plugin for Java project to upload files to AWS S3.'
    tags = ['AWS', 'S3', 'Java']

  plugins {
    javaS3Plugin {
      id = 'com.yantranet.java-s3'
      displayName = 'java S3 Uploader'
    }
  }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact source:'./build/libs/' + rootProject.name + '-' + rootProject.version + '.jar' , extension:'jar'
            groupId = 'com.yantranet.gradle'
            artifactId = 'java-s3'

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.compile.allDependencies.each {
                    if (it.name != "unspecified") {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}
